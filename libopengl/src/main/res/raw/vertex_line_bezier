attribute float a_Data;
uniform vec4 u_StartEndData;
uniform vec4 u_ControlData;
uniform mat4 u_MVPMatrix;
uniform float u_Amp;

vec2 bezier3(in vec2 p0, in vec2 p1, in vec2 p2, in vec2 p3, in float t) {
    vec2 q0 = mix(p0, p1, t);
    vec2 q1 = mix(p1, p2, t);
    vec2 q2 = mix(p2, p3, t);

    vec2 r0 = mix(q0, q1, t);
    vec2 r1 = mix(q1, q2, t);
    return mix(r0, r1, t);
}

void main() {
    vec4 pos;
    pos.w = 1.0;

    vec2 p0 = u_StartEndData.xy;
    vec2 p3 = u_StartEndData.zw;

    vec2 p1 = u_ControlData.xy;
    vec2 p2 = u_ControlData.zw;

    p0.y *= u_Amp;
    p1.y *= u_Amp;
    p2.y *= u_Amp;
    p3.y *= u_Amp;

    float t = a_Data;

    vec2 point = bezier3(p0, p1, p2, p3, t);
    pos.xy = point;

    gl_Position = u_MVPMatrix * pos;
    gl_PointSize = 20.0;
}